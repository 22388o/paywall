// Gradle file containing all common compilation of documentation when generating site.

apply plugin: 'org.ajoberstar.git-publish'

task aggregatedJavadoc(type: Javadoc) {
    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    destinationDir = new File(buildDir, 'docs/javadoc')
    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}

task aggregatedTestReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task ('doc',type: Copy,dependsOn: [":paywall-doc:asciidoctor", ":paywall-js:grunt_jsdoc", aggregatedJavadoc, aggregatedTestReport]){
    from('paywall-js/build/'){
        include('jsdoc/**')
        include('dist/**')
    }
    from('paywall-spring/build/resources/main'){
        include('**.xsd')
    }
    from('build/docs/'){
        include('javadoc/**')
    }
    from('build/reports/'){
        include('allTests/**')
    }
    from('LICENSE.txt'){
    }
    from('paywall-doc/docs'){
        include('CNAME')
    }
    from('paywall-doc/docs'){
        include('lightningj-release-pubkey.asc')
    }
    into('paywall-doc/build/docs/html5')
}

// Publishing web page to git hub pages
gitPublish {
    repoUri = 'https://github.com/lightningj-org/paywall.git'
    branch = 'gh-pages'
    contents {
        from 'paywall-doc/build/docs/html5'
    }
    commitMessage = 'Publishing a new page' // defaults to 'Generated by gradle-git-publish'
}

