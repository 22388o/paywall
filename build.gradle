

allprojects{
  repositories {
    mavenCentral()
  }

}

subprojects{
  group 'org.lightningj'
  version = "0.0.1"

  apply plugin: "java"
  apply plugin: 'signing'
  apply plugin: 'maven'
  apply plugin: "groovy"
  apply plugin: 'idea'
  apply plugin: 'project-report'
  sourceCompatibility = 1.8

  dependencies{
    // mandatory dependencies for using Spock
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"

    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "net.bytebuddy:byte-buddy:1.9.7"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.5.1"    // allows mocking of classes without default constructor (together with CGLIB)
  }

  /**
   * Test to be run prior to release to run all test suites. Needs a configured LND node for integration test.
   */
  task preReleaseTest(dependsOn: ["check",":paywall-core:integrationTest",":paywall-springboot2:functionalTest"], group: "verification"){}

  /*
  Adds generated source directories to be included in javadoc generation.
  */
  javadoc {
    failOnError= false
  }

  test{
    systemProperty "java.net.preferIPv4Stack", "true"
    systemProperty "crypto.policy", "unlimited"
//    testLogging { // Uncomment to get more descriptive error messages in travis CI.
//      events "passed", "skipped", "failed"
//      exceptionFormat "full"
//    }
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar, sourcesJar
  }
}

