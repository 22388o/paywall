
sourceSets{
    integrationTest {

        java{
            srcDirs = ['src/integration-test/java']
        }

        resources{
            srcDirs = ['src/integration-test/resources']
        }

        groovy{
            srcDirs = ['src/integration-test/groovy']
        }


        compileClasspath += project.sourceSets.main.output
        runtimeClasspath += project.sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testCompile
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test, group: "verification", description: "Runs integration tests using live LND node."){
    doFirst{
        def host = project.hasProperty("paywall.integration.test.lnd.host") ? project.property("paywall.integration.test.lnd.host") : null
        def port = project.hasProperty("paywall.integration.test.lnd.port") ? project.property("paywall.integration.test.lnd.port") : null
        def tlsCert = project.hasProperty("paywall.integration.test.lnd.tlscertpath") ? project.property("paywall.integration.test.lnd.tlscertpath")  : null
        def macaroon = project.hasProperty("paywall.integration.test.lnd.macaroonpath") ? project.property("paywall.integration.test.lnd.macaroonpath")  : null
        if(!host || !port || !tlsCert || !macaroon){
            println """LND Settings missing in gradle properies. Please update with following properties:

  paywall.integration.test.lnd.host=<host to connect to>
  paywall.integration.test.lnd.port=<port>
  paywall.integration.test.lnd.tlscertpath=<Path to TLS Cert>
  paywall.integration.test.lnd.macaroonpath=<Path to Macaroon>
"""
            System.exit(-1)
        }else{
            println """Using LND Settings:
    host             : ${host}
    port             : ${port}
    Path to TLS Cert : ${tlsCert}
    Path to Macaroon : ${macaroon}
"""
            systemProperty "paywall.integration.test.lnd.host",host
            systemProperty "paywall.integration.test.lnd.port",port
            systemProperty "paywall.integration.test.lnd.tlscertpath",tlsCert
            systemProperty "paywall.integration.test.lnd.macaroonpath",macaroon
        }
    }
    testClassesDirs = project.sourceSets.integrationTest.output.classesDirs
    classpath = project.sourceSets.integrationTest.runtimeClasspath
}

integrationTest.shouldRunAfter test

/*
  Task for specifying for Intellij which source directories to use.
 */
idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/groovy')
        testResourceDirs += file('src/integration-test/resources')

        contentRoot = project.projectDir
    }
}
